PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX fx:  <http://sparql.xyz/facade-x/ns/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX mei: <http://www.music-encoding.org/ns/mei#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX apf: <http://jena.apache.org/ARQ/property#>

SELECT DISTINCT ?identifier ?genre ?author ?authority ?authURI
WHERE {
  SERVICE <x-sparql-anything:location=CNW/cnw0001.xml,blank-nodes=false> {
	?bibl a mei:bibl ;
	 ?s1 [ a mei:genre ; rdf:_1 ?genre ] ;
	 ?s2 [ a mei:author ; rdf:_1 ?author ] ;
	 ?s3 [ a mei:physLoc ; 
	 	rdf:_1 [ a mei:repository ; rdf:_1 [ a mei:identifier ; xyz:authURI ?authURI ; xyz:authority ?authority ] ] ;
	  	rdf:_2 [ a mei:identifier ; rdf:_1 ?identifier ] 
	 ]
  }
}